Class WW2Player : DoomPlayer
{
	Default
	{
		Player.MaxHealth 100;
		Player.DisplayName "Marine";
		Player.CrouchSprite "PLYC";
		Player.StartItem "Luger";
		Player.StartItem "Knife";
		Player.StartItem "LugerLoaded", 8;
		Player.StartItem "Ammo9mm", 40;
		Player.StartItem "AmmoGranate", 2;
		Player.StartItem "AmmoTNT", 1;
		Player.WeaponSlot 1, "Knife";
		Player.WeaponSlot 2, "Luger", "Walther";
		Player.WeaponSlot 3, "TrenchGun", "BA5";
		Player.WeaponSlot 4, "MP40", "STEN";
		Player.WeaponSlot 5, "G43", "Kar98k";
		Player.WeaponSlot 6, "Flammenwerfer";
		Player.WeaponSlot 7, "UMG43";
	}

	Override Void PostBeginPlay()
	{
		Super.PostBeginPlay();

		Int sk = G_SkillPropertyInt(SKILLP_ACSReturn);
		If(sk == 4 && CVar.FindCVar('Acid_Health').GetBool()) { A_SetHealth(999); }
		Else { A_SetHealth(Health); }
	}

    Int hpDecayTimer; // OverHealth
    Int painTimer;

    Override Void Tick()
    {
        Super.Tick();
        If(Health <= 0) return;

		If(CVar.FindCVar('Acid_Health').GetBool())
		{
			hpDecayTimer++;
			If(painTimer) painTimer--;

			If(InStateSequence(CurState,ResolveState("Pain"))) // If hurt, wait 2 seconds before regeneration
			{
				painTimer = 70;
			}
			// Slowly remove HP if over maximum.
			If((Health > 500 && hpDecayTimer >= 5) || (Health > 250 && hpDecayTimer >= 10) || (Health > 100 && hpDecayTimer >= 20))
			{
				A_SetHealth(Health - 1);
				hpDecayTimer = 0;
			}
			// Slowly gain HP if less than 100 health, every 5 tics.
			// Only gain HP if we've not been hit recently.
			If(painTimer <= 0 && hpDecayTimer >= 5 && Health < MaxHealth)
			{
				 A_SetHealth(Health + 1);
				 hpDecayTimer = 0;
			}
			Else If(!(Health%20) && Health <= 100) 
			{
				// If health is evenly divisible by 20 and not greater than 100.
				hpDecayTimer = 0;
			}
		}
    }

	Override Void CheatGive(String name, int amount)
	{
		Super.CheatGive(name, amount);
		If(name ~== "all" || name ~== "armor") 
		{
			CheatTake("armor", 1);
			Super.CheatGive("FlakJacket", 1);
		}
		If(name ~== "megasphere")
		{
			CheatTake("armor", 1);
			Super.CheatGive("MegaCharge", 1);
		}
	}
}
