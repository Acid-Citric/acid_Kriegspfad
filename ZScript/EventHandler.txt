Class WW2Handler : EventHandler
{
	Override Void PlayerRespawned(PlayerEvent e)
	{
		PlayerInfo player = players[e.PlayerNumber];
		player.mo.TakeInventory("FireDeathEffect", 1);
	}
    Override Void WorldThingDied(WorldEvent e) // Credits to jarewill on the ZDoom Forums
	{
   	     If(e.Inflictor && e.Inflictor.DamageType == "Fire") { e.Thing.GiveInventory("FireDeathEffect",1); }
	}
    Override Void WorldThingRevived(WorldEvent e) { e.Thing.TakeInventory("FireDeathEffect",1); }

	Override void WorldThingSpawned(WorldEvent e) { If(e.Thing.bISMONSTER && (e.Thing is "LostSoul") || (e.Thing is "CommanderKeen")) { e.Thing.bNOBLOOD = True; } }

	Override Void CheckReplacement(ReplaceEvent e)
	{
		class<Actor> cls = e.Replacee;

		If(cls == 'Fist')											e.Replacement = "Knife";
		Else If(cls == 'Chainsaw')									e.Replacement = "Walther";
		Else If(cls == 'Pistol')									e.Replacement = "Luger";
		Else If(cls == 'Shotgun')									e.Replacement = "TrenchGun";
		Else If(cls == 'SuperShotgun')								e.Replacement = "BA5";
		Else If(cls == 'Chaingun')
		{
			If(Random[Chaingun](0,1)) e.Replacement = 'MP40';
			Else e.Replacement = 'STEN';
		}
		Else If(cls == 'RocketLauncher')
		{
			If (Random[RocketLauncher](0,1)) e.Replacement = 'Kar98k';
			Else e.Replacement = 'G43';
		}
		Else If(cls == 'PlasmaRifle')								e.Replacement = "Flammenwerfer";
		Else If(cls == 'BFG9000')									e.Replacement = "UMG43";

		Else If((cls == 'HealthBonus') || (cls == 'ArmorBonus'))
		{
			If(Random(0,19) == 0)									e.Replacement = "Small_HealthPack";
			Else If(Random(0,19) == 19)								e.Replacement = "AmmoGranate";
			Else If(Random(0,19) == 9)								e.Replacement = "AmmoTNT";			
			Else e.Replacement = 'Nothing';
		}

		Else If(cls == 'Stimpack')									e.Replacement = "Medium_HealthPack";
		Else If(cls == 'Medikit')									e.Replacement = "Large_HealthPack";

		Else If(cls == 'GreenArmor')								e.Replacement = "ArmoredHelmet";
		Else If(cls == 'BlueArmor')									e.Replacement = "FlakJacket";	

		Else If(cls == 'SoulSphere')
		{
			If (Random[SoulSphere](0,1)) e.Replacement = 'SuperCharge';
			Else e.Replacement = 'Health_1938_Latour';
		}
		Else If(cls == 'MegaSphere')								e.Replacement = "MegaCharge";
		Else If(cls == 'Berserk')									e.Replacement = "BerserkSphere";


		Else If(cls == "RadSuit")									e.Replacement = "HazmatSuit";
		Else If(cls == 'Infrared')									e.Replacement = "NightVision";
		Else If(cls == 'AllMap')									e.Replacement = "Blueprints";
		Else If(cls == 'InvulnerabilitySphere')						e.Replacement = "InvulnSphere";
		Else If(cls == 'BlurSphere')								e.Replacement = "InvisSphere";


		Else If(cls == 'Clip')
		{
			If (Random[Clip](0,19) == 19) e.Replacement = 'Walther';
			Else e.Replacement = 'Ammo9mm_1';
		}
		Else If(cls == 'ClipBox')
		{
			If (Random[ClipBox](0,1)) e.Replacement = 'Ammo9mm_2';
			Else e.Replacement = 'Ammo9mm_3';
		}
		Else If(cls == 'Shell')
		{
			If(Random[Shell](0,1))	e.Replacement = "Ammo12G_1";
			Else e.Replacement = 'Ammo9mm_1';
		}
		Else If(cls == 'ShellBox')
		{
			If(Random[ShellBox](0,1))	e.Replacement = "Ammo12G_2";
			Else e.Replacement = 'ClipBox';
		}
		Else If(cls == 'RocketAmmo')
		{
			If (Random[RocketAmmo](0,1)) e.Replacement = 'Ammo792M_1';
			Else e.Replacement = 'Ammo9mm_1';
		}
		Else If(cls == 'RocketBox')
		{
			If (Random[RocketBox](0,1)) e.Replacement = 'Ammo792M_2';
			Else e.Replacement = 'ClipBox';
		}
		Else If(cls == 'Cell')
		{
			If (Random[Cell](0,1)) e.Replacement = 'Ammo762M_1';
			Else e.Replacement = 'Ammo9mm_1';
		}
		Else If(cls == 'CellPack')
		{
			If (Random[CellPack](0,1)) e.Replacement = 'AmmoFuel_1';
			Else e.Replacement = 'ClipBox';
		}
		Else If(cls == 'Backpack')									e.Replacement = "AmmoPouches";


        String checksum = level.GetChecksum();
        If(level.GetChecksum() == "3ff94e27423f91c1585b3396f0c03459") // doom2.wad MAP31
        {
			If(cls == "HealthBonus") { e.Replacement = "Wolfenstein3D_DogFood"; }
            Else If(cls == "Stimpack") { e.Replacement = "Wolfenstein3D_Meal"; }
			Else If(cls == "Medikit") { e.Replacement = "Wolfenstein3D_FirstAidKit"; }
			Else If(cls == "Berserk") { e.Replacement = "Wolfenstein3D_Serum"; }
        }
		If(level.GetChecksum() == "aa4ca3fc891d13821accabd836e29eb5") // doom2.wad MAP32 
        {
            If(cls == "Medikit") { e.Replacement = "Wolfenstein3D_FirstAidKit"; }
			Else If(cls == "Berserk") { e.Replacement = "Wolfenstein3D_Serum"; }
        }
	}
}

Class FireDeathEffect : Inventory // Credits to jarewill on the ZDoom Forums
{
	uint oldTranslation;

	Override Void AttachToOwner(Actor other)
	{
        oldTranslation = other.Translation;
		Super.AttachToOwner(other);
	}
	Override Void DetachFromOwner()
    {
        Owner.Translation = oldTranslation;
    }

	Override Void DoEffect()
	{
		Super.DoEffect();
		If(Owner && !(Owner is "ExplosiveBarrel") && !(Owner is "LostSoul") && !(Owner is "PainElemental") && !(Owner is "CyberDemon") && !(Owner is "SpiderMastermind") && !Owner.FindState("Death.Fire", True) && !Owner.FindState("Burn", True))
		{
			Owner.bFLY = False; Owner.bNOGRAVITY = False; Owner.bNOBLOOD = True; // Set relevant flags so the monster cannot be damaged, can't fly and doesn't spawn blood.
			Owner.height = self.height; //Reset the height
			Owner.A_SetTranslation("Scorched"); //Set a color translation
		}
	}
}

Class KriegspfadLevelCompatibility : LevelPostProcessor
{
	protected void Apply(Name checksum, String mapname)
	{
		int nhbonus = 0;
		switch (checksum)
		{
		case 'none':
			return;

		// Doom 2 MAP31
		case '3ff94e27423f91c1585b3396f0c03459':
			// remove excess healthbonuses (4 of them)
			for(int i=0; i<GetThingCount(); i++)
			{
				if(GetThingEdNum(i) != 2014) continue;
				if(nhbonus) SetThingEdNum(i,0);
				nhbonus++;
			}
			break;
		}
	}
}
