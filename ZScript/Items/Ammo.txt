mixin Class AmmoPickupMessage
{
	Default
	{
		Scale 0.75;
		Inventory.PickupSound "misc/ammo_pkup";
	}
	Override String PickupMessage()
	{
		double factor = G_SkillPropertyFloat(SKILLP_AmmoFactor) * (bDROPPED ? -G_SkillPropertyFloat(SKILLP_DropAmmoFactor) : 1);
		int result = int(Amount * factor);
		if(result == 1) return String.Format("+%d %s",result,GetTag());
		else return String.Format("+%d %ss",result,GetTag());	
	}
}
Class Ammo9mm : Ammo
{
	mixin AmmoPickupMessage;
	Default
	{
		Tag "$TAG_AMMO_9MM";
		Inventory.Icon "9MM_A0";
		Inventory.MaxAmount 240;
		Ammo.BackpackAmount 8;
		Ammo.BackpackMaxAmount 480;
	}
	Override Void PostBeginPlay()
	{
		Super.PostBeginPlay();
		If(bTOSSED && Random(0,1)) { A_DropItem("AmmoGranate",1,16); A_DropItem("AmmoTNT",1,4); }
		Else If(!bTOSSED && Random(0,1)) { A_SpawnItemEx("AmmoGranate",Random(-8,8),Random(-8,8),failchance: 240); A_SpawnItemEx("AmmoTNT",Random(-8,8),Random(-8,8),failchance: 252); }
	}
}

Class Ammo9mm_1 : Ammo9mm
{
	Override Void PostBeginPlay()
	{
		Super.PostBeginPlay();
		bSPRITEFLIP = Random(0,1);
		Amount = Random[Ammo9mm](1,8);
		
		double factor = G_SkillPropertyFloat(SKILLP_AmmoFactor) * (bDROPPED ? -G_SkillPropertyFloat(SKILLP_DropAmmoFactor) : 1);
		int result = int(Amount * factor);
		If(result > 8 && G_SkillPropertyFloat(SKILLP_AmmoFactor) > 1) { bSPRITEFLIP = False; SetState(ResolveState("Spawn")+2); }
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay;
		9MM_ AB -1;
		Stop;
	}
}
Class Ammo9mm_2 : Ammo9mm
{
	Override Void PostBeginPlay()
	{
		Super.PostBeginPlay();
		Amount = Random[Ammo9mm](8,16);
	}
	States
	{
	Spawn:
		9MM_ B -1;
		Stop;
	}
}
Class Ammo9mm_3 : Ammo9mm
{
	Default	{ XScale 0.65; }
	Override Void PostBeginPlay()
	{
		Super.PostBeginPlay();
		Amount = Random[Ammo9mm](8,32);
	}
	States
	{
	Spawn:
		9MM_ C -1;
		Stop;
	}
}

Class Ammo12G : Ammo
{
	mixin AmmoPickupMessage;
	Default	
	{
		Tag "$TAG_AMMO_12GA";
		Inventory.Icon "12G_A0";
		Inventory.MaxAmount 32;
		Ammo.BackpackAmount 4;
		Ammo.BackpackMaxAmount 64;
	}
	Override Void PostBeginPlay()
	{
		Super.PostBeginPlay();
		If(bTOSSED && Random(0,1)) { A_DropItem("AmmoGranate",1,16); A_DropItem("AmmoTNT",1,4); }
		Else If(!bTOSSED && Random(0,1)) { A_SpawnItemEx("AmmoGranate",Random(-8,8),Random(-8,8),failchance: 240); A_SpawnItemEx("AmmoTNT",Random(-8,8),Random(-8,8),failchance: 252); }
	}
}
Class Ammo12G_1 : Ammo12G
{
	Override Void PostBeginPlay()
	{
		Super.PostBeginPlay();
		bSPRITEFLIP = Random(0,1);
		// variants
		int dec = Random[Ammo12G](0,3);
		switch(dec)
		{
		case 0:
		default:
			SetState(ResolveState("Spawn"));
			Amount = 4;
			break;
		case 1:
			SetState(ResolveState("Spawn")+2);
			Amount = 3;
			break;
		case 2:
			SetState(ResolveState("Spawn")+3);
			Amount = 2;
			break;
		case 3:
			SetState(ResolveState("Spawn")+4);
			Amount = 1;
			break;
		}
		double factor = G_SkillPropertyFloat(SKILLP_AmmoFactor) * (bDROPPED ? -G_SkillPropertyFloat(SKILLP_DropAmmoFactor) : 1);
		int result = int(Amount * factor);
		If(result == 2 && G_SkillPropertyFloat(SKILLP_AmmoFactor) > 1) { SetState(ResolveState("Spawn")+3); }
		Else If(result == 4 && G_SkillPropertyFloat(SKILLP_AmmoFactor) > 1) { SetState(ResolveState("Spawn")); }
		Else If(result > 4 && G_SkillPropertyFloat(SKILLP_AmmoFactor) > 1) { bSPRITEFLIP = False; SetState(ResolveState("Spawn")+5); }
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay;
		12G_ ABCDE -1;
		Stop;
	}
}
Class Ammo12G_2 : Ammo12G
{
	Override Void PostBeginPlay()
	{
		Super.PostBeginPlay();
		Amount = Random[Ammo12G](4,12);
	}
	States
	{
	Spawn:
		12G_ E -1;
		Stop;
	}
}

Class Ammo792M : Ammo
{
	mixin AmmoPickupMessage;
	Default
	{
		Tag "$TAG_AMMO_792M";
		Inventory.Icon "792MA0";
		Inventory.MaxAmount 40;
		Ammo.BackpackAmount 5;
		Ammo.BackpackMaxAmount 100;
		Scale 1.0;
	}
	Override Void PostBeginPlay()
	{
		Super.PostBeginPlay();
		If(bTOSSED && Random(0,1)) { A_DropItem("AmmoGranate",1,16); A_DropItem("AmmoTNT",1,4); A_DropItem("AmmoRifleGrenade",1,256); }
		Else If(!bTOSSED && Random(0,1))
		{ 
			A_SpawnItemEx("AmmoGranate",Random(-8,8),Random(-8,8),failchance: 240);
			A_SpawnItemEx("AmmoTNT",Random(-8,8),Random(-8,8),failchance: 252);
			A_SpawnItemEx("AmmoRifleGrenade",Random(-8,8),Random(-8,8));
		}
	}
}
Class Ammo792M_1 : Ammo792M
{
	Override Void PostBeginPlay()
	{
		super.PostBeginPlay();
		bSPRITEFLIP = Random(0,1);
		// variants
		int dec = Random[Ammo12G](0,4);
		switch(dec)
		{
		case 0:
		default:
			SetState(ResolveState("Spawn"));
			Amount = 5;
			break;
		case 1:
			SetState(ResolveState("Spawn")+2);
			Amount = 4;
			break;
		case 2:
			SetState(ResolveState("Spawn")+3);
			Amount = 3;
			break;
		case 3:
			SetState(ResolveState("Spawn")+4);
			Amount = 2;
			break;
		case 4:
			SetState(ResolveState("Spawn")+5);
			Amount = 1;
			break;
		}
		double factor = G_SkillPropertyFloat(SKILLP_AmmoFactor) * (bDROPPED ? -G_SkillPropertyFloat(SKILLP_DropAmmoFactor) : 1);
		int result = int(Amount * factor);
		If(result == 2 && G_SkillPropertyFloat(SKILLP_AmmoFactor) > 1) { SetState(ResolveState("Spawn")+4); }
		Else If(result == 4 && G_SkillPropertyFloat(SKILLP_AmmoFactor) > 1) { SetState(ResolveState("Spawn")+2); }
		Else If(result > 4 && G_SkillPropertyFloat(SKILLP_AmmoFactor) > 1) { bSPRITEFLIP = False; SetState(ResolveState("Spawn")+6); }
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay;
		792M ABCDEF -1;
		Stop;
	}
}
Class Ammo792M_2 : Ammo792M
{
	Override Void PostBeginPlay()
	{
		Super.PostBeginPlay();
		Amount = Random[Ammo792M](5,15);
	}
	States
	{
	Spawn:
		792M F -1;
		Stop;
	}
}

Class Ammo762M : Ammo
{
	mixin AmmoPickupMessage;
	Default
	{
		Tag "$TAG_AMMO_762M";
		Inventory.Icon "762MA0";
		Inventory.MaxAmount 500;
		Ammo.BackpackMaxAmount 500;
		Scale 0.875;
	}
	Override Void PostBeginPlay()
	{
		Super.PostBeginPlay();
		If(bTOSSED && Random(0,1)) { A_DropItem("AmmoGranate",1,16); A_DropItem("AmmoTNT",1,4); }
		Else If(!bTOSSED && Random(0,1)) { A_SpawnItemEx("AmmoGranate",Random(-8,8),Random(-8,8),failchance: 240); A_SpawnItemEx("AmmoTNT",Random(-8,8),Random(-8,8),failchance: 252); }
	}
}
Class Ammo762M_1 : Ammo762M replaces Cell
{
	Override Void PostBeginPlay()
	{
		Super.PostBeginPlay();
		Amount = Random[Ammo762M](10,50);
	}
	States
	{
	Spawn:
		762M A -1;
		Stop;
	}
}

Class AmmoFuel : Ammo
{
	mixin AmmoPickupMessage;
	Default
	{
		Tag "$TAG_AMMO_FUEL";
		Inventory.Icon "FUELA0";
		Inventory.MaxAmount 150;
		Ammo.BackpackMaxAmount 150;
		Scale 1.0;
	}
	Override Void PostBeginPlay()
	{
		Super.PostBeginPlay();
		If(bTOSSED && Random(0,1)) { A_DropItem("AmmoGranate",1,16); A_DropItem("AmmoTNT",1,4); }
		Else If(!bTOSSED && Random(0,1)) { A_SpawnItemEx("AmmoGranate",Random(-8,8),Random(-8,8),failchance: 240); A_SpawnItemEx("AmmoTNT",Random(-8,8),Random(-8,8),failchance: 252); }
	}
}
Class AmmoFuel_1 : AmmoFuel
{
	Override Void PostBeginPlay()
	{
		Super.PostBeginPlay();
		Amount = Random[AmmoFuel](10,50);
	}
	States
	{
	Spawn:
		FUEL A -1;
		Stop;
	}
}

Class AmmoRifleGrenade : Ammo
{
	Default
	{
		Tag "$TAG_AMMO_RIFLEGRENADE";
		Inventory.Icon "R_G_A0";
		Inventory.Amount 1;
		Inventory.MaxAmount 10;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 15;
		Ammo.DropAmount 1;
		+INVENTORY.IGNORESKILL
	}
	Override String PickupMessage()	{ return String.Format("+%d %s",Amount,GetTag()); }
	Override Void PostBeginPlay()
	{
		Super.PostBeginPlay();
		bSPRITEFLIP = Random(0,1);
		SetState(ResolveState("Spawn")+Random[AmmoRifleGrenade](1,2));
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay;
		R_G_ AB -1;
		Stop;
	}
}

Class AmmoTNT : Ammo
{
	int counter;
	Default
	{
		Tag "$TAG_AMMO_TNT";
		Inventory.Icon "TNT_A0";
		Inventory.Amount 1;
		Inventory.MaxAmount 5;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 5;
		Ammo.DropAmount 1;
		Scale 1.0;
		+INVENTORY.IGNORESKILL
	}
	Override String PickupMessage()	{ return String.Format("+%d %s",Amount,GetTag()); }
	Override Void PostBeginPlay()
	{
		counter = 5;
		Super.PostBeginPlay();
	}
    Override void DoEffect()
    {
        Super.DoEffect();
        If(Owner is "PlayerPawn" && Owner.GetPlayerInput(MODINPUT_BUTTONS) & BT_USER2 && !Owner.FindInventory("AmmoTNT_TimerCooldown"))
		{
			If(counter == 45) { counter = 5; Owner.A_GiveInventory("AmmoTNT_TimerCooldown"); }
			Else { counter += 5; Owner.A_GiveInventory("AmmoTNT_TimerCooldown"); }
		}
    }
	Override Bool Use(bool pickup)
	{
		If(Owner && Self.Amount > 0 && !Owner.FindInventory("AmmoTNT_AmmoCooldown"))
		{
			If(counter == 45) { Owner.A_SpawnProjectile("Dynamite_Thrown_45",32,0,0,CMF_AIMDIRECTION,Owner.Pitch-16.0); }
			Else If(counter == 40) { Owner.A_SpawnProjectile("Dynamite_Thrown_40",32,0,0,CMF_AIMDIRECTION,Owner.Pitch-16.0); }
			Else If(counter == 35) { Owner.A_SpawnProjectile("Dynamite_Thrown_35",32,0,0,CMF_AIMDIRECTION,Owner.Pitch-16.0); }
			Else If(counter == 30) { Owner.A_SpawnProjectile("Dynamite_Thrown_30",32,0,0,CMF_AIMDIRECTION,Owner.Pitch-16.0); }
			Else If(counter == 25) { Owner.A_SpawnProjectile("Dynamite_Thrown_25",32,0,0,CMF_AIMDIRECTION,Owner.Pitch-16.0); }
			Else If(counter == 20) { Owner.A_SpawnProjectile("Dynamite_Thrown_20",32,0,0,CMF_AIMDIRECTION,Owner.Pitch-16.0); }
			Else If(counter == 15) { Owner.A_SpawnProjectile("Dynamite_Thrown_15",32,0,0,CMF_AIMDIRECTION,Owner.Pitch-16.0); }
			Else If(counter == 10) { Owner.A_SpawnProjectile("Dynamite_Thrown_10",32,0,0,CMF_AIMDIRECTION,Owner.Pitch-16.0); }
			Else If(counter == 5) { Owner.A_SpawnProjectile("Dynamite_Thrown_5",32,0,0,CMF_AIMDIRECTION,Owner.Pitch-16.0); }
			
			Owner.A_GiveInventory("AmmoTNT_AmmoCooldown");
			
			Self.Amount --;
		}
		Return 0;
	}
	States
	{
	Spawn:
		TNT_ A -1;
		Stop;
	}
}
Class AmmoTNT_TimerCooldown : Powerup { Default { Powerup.Duration 10; } }
Class AmmoTNT_AmmoCooldown : Powerup { Default { Powerup.Duration 20; } }

Class AmmoGranate : Ammo
{
	bool shgmod;
	Default
	{
		Tag "$TAG_AMMO_GRANATE";
		Inventory.Icon "SHG_A0";
		Inventory.Amount 1;
		Inventory.MaxAmount 10;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 15;
		Ammo.DropAmount 1;
		Scale 0.75;
		+INVENTORY.IGNORESKILL
	}
	Override String PickupMessage()	{ return String.Format("+%d %s",Amount,GetTag()); }
	Override Void PostBeginPlay()
	{
		shgmod = False;
		Super.PostBeginPlay();
		bSPRITEFLIP = Random(0,1);
		SetState(ResolveState("Spawn")+Random[AmmoGranate](1,2));
	}
    Override void DoEffect()
    {
        Super.DoEffect();
        If(Owner is "PlayerPawn" && Owner.GetPlayerInput(MODINPUT_BUTTONS) & BT_USER1 && !Owner.FindInventory("AmmoGranate_TimerCooldown"))
        {
			If(shgmod == True) { shgmod = False; Owner.A_GiveInventory("AmmoGranate_TimerCooldown"); }
			Else { shgmod = True; Owner.A_GiveInventory("AmmoGranate_TimerCooldown"); }
		}
    }
	Override Bool Use(bool pickup)
	{
		If(Owner && Self.Amount > 0 && !Owner.FindInventory("AmmoGranate_AmmoCooldown"))
		{
			If(shgmod == True && Self.Amount >= 5)
			{
				Owner.A_SpawnProjectile("Granate_Thrown_B",32,0,0,CMF_AIMDIRECTION,Owner.Pitch-16.0);
				Owner.A_GiveInventory("AmmoGranate_AmmoCooldown");

				Self.Amount -= 5;
			}
			Else
			{ 
				shgmod = False;

				Owner.A_SpawnProjectile("Granate_Thrown_A",32,0,0,CMF_AIMDIRECTION,Owner.Pitch-16.0);
				Owner.A_GiveInventory("AmmoGranate_AmmoCooldown");

				Self.Amount --;
			}
		}
		Return 0;
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay;
		SHG_ AB -1;
		Stop;
	}
}
Class AmmoGranate_TimerCooldown : Powerup { Default { Powerup.Duration 10; } }
Class AmmoGranate_AmmoCooldown : Powerup { Default { Powerup.Duration 20; } }

Class AmmoPouches : BackpackItem
{
	Default	{ Inventory.PickupMessage "$PICKUP_AMMO_POUCHES"; }
	States
	{
	Spawn:
		AMM0 A -1;
		Stop;
	}
}
