Class Kar98k : AxisWeapon
{
	Bool ADS, riflereload, riflelower;
	Default
	{
		Weapon.SelectionOrder 2500;

		Weapon.AmmoType "Kar98kLoaded";
		Weapon.AmmoType2 "Ammo792M";
		Inventory.PickupMessage "You got the Kar98k!";
		Tag "$TAG_WEAPON_KAR98K";
		Weapon.UpSound "mauser/select";
	}
	Override Void AttachToOwner(Actor other)
	{
		Super.AttachToOwner(other);
		If(bTOSSED) { other.A_GiveInventory("Kar98kLoaded", random(1,3)); }
		Else { other.A_GiveInventory("Kar98kLoaded", random(1,5)); }
	}
   	Override Bool TryPickup(in out Actor toucher)
    {
		If(toucher)
		{
			Let weap = toucher.FindInventory("Kar98k");
			If(!weap) { Self.AmmoGive2 = 0; }
			Else If(bTOSSED) { Self.AmmoGive2 = random(1,5); }
			Else { Self.AmmoGive2 = random(1,5); }
		}
			Return Super.TryPickup(toucher);
	}
	States
	{
	Spawn:
		WPAP A -1;
		Stop;
	Select:
		WPAG D 0 A_JumpIf(!CountInv("Kar98kLoaded"), 2);
		WPAG A 0;
		#### # 1 A_Raise(12);
		Loop;
	Ready:
		WPAG A 0 A_JumpIf(Invoker.ADS == True, 4);
		#### D 0 A_JumpIf(!CountInv("Kar98kLoaded"), 2);
		#### A 0;
		WPAG # 1 A_WeaponReady(WRF_ALLOWRELOAD);
		Loop;
		#### R 1 
		{
			A_WeaponReady(WRF_ALLOWRELOAD|WRF_NOBOB);
			A_ZoomFactor(5.0, ZOOM_INSTANT);
			A_WeaponOffset(FRandom[Kar98k](-0.25,0.25), WEAPONTOP-FRandom[Kar98k](-0.25,0.25), WOF_INTERPOLATE);
		}
		Loop;
	Deselect:
		TNT1 A 0 { Invoker.riflelower = True; }
		TNT1 A 0 A_JumpIf(Invoker.riflereload, "ReloadFinish");
		WPAG R 0 A_JumpIf(Invoker.ADS, "LowerZoom");
		WPAG D 0 A_JumpIf(!CountInv("Kar98kLoaded"), 2);
		WPAG A 0;
		#### # 0 { Invoker.riflelower = False; }
		#### # 1 A_Lower(12);
		Loop;

	Fire:
		TNT1 A 0 A_JumpIf(!CountInv("Kar98kLoaded"), "Reload");
		TNT1 A 0 A_JumpIf(Invoker.riflereload, "ReloadFinish");
		WPAG B 1
		{
			If(Invoker.ADS == False) { A_GunFlash(); }
			A_AlertMonsters();
			A_FireBullets(0, 0, 1, random(130,200), "BulletPuff_2", FBF_NORANDOMPUFFZ);
			A_StartSound("mauser/fire", CHAN_WEAPON);
			A_TakeInventory("Kar98kLoaded",1);
			
			If(Invoker.ADS == True)
			{
				 sprite = GetSpriteIndex("WPAG");
				 frame = 17; //Translates to letter R. 0 is A, 1 is B, 2 is C, etc.
			}
		}
	Reloading:
		#### C 1 { A_WeaponOffset(0,44,WOF_INTERPOLATE); A_ZoomFactor(1.0, ZOOM_INSTANT); }
		#### D 1 A_WeaponOffset(0,40,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(0,38,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(0,36,WOF_INTERPOLATE);
		#### D 0 A_JumpIf(!CountInv("Kar98kLoaded"), "Empty");
		#### E 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		#### F 1 { A_WeaponOffset(0,32,WOF_INTERPOLATE); A_StartSound("mauser/cock", CHAN_5); }
		#### G 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		#### H 1 A_WeaponOffset(0,34,WOF_INTERPOLATE);
		#### H 1 A_WeaponOffset(0,36,WOF_INTERPOLATE);
		#### I 1 A_WeaponOffset(0,38,WOF_INTERPOLATE);
		#### I 1 A_WeaponOffset(0,40,WOF_INTERPOLATE);
		#### J 7 A_SpawnItemEx("Casing_MauserRifle",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION);	
		#### M 1 A_WeaponOffset(0,39,WOF_INTERPOLATE);
		#### M 1 A_WeaponOffset(0,38,WOF_INTERPOLATE);
		#### N 1 A_WeaponOffset(0,37,WOF_INTERPOLATE);
		#### N 1 A_WeaponOffset(0,36,WOF_INTERPOLATE);
		#### O 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		#### P 1 A_WeaponOffset(0,34,WOF_INTERPOLATE);
		#### Q 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		#### A 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		Goto Ready;
	Empty:
		#### D 3 { A_WeaponOffset(0,36,WOF_INTERPOLATE); A_ZoomFactor(1.0, ZOOM_INSTANT); Invoker.ADS = False; }
		#### D 3 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		#### D 3 A_WeaponOffset(0,34,WOF_INTERPOLATE);
		#### D 3 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		#### D 10 { A_WeaponOffset(0,32,WOF_INTERPOLATE); A_WeaponReady(WRF_NOFIRE); }
		Goto Ready;
	Flash:
		WPAF A 1 BRIGHT A_Light2;
		WPAF B 1 BRIGHT A_Light1;
		Goto LightDone;

	AltFire:
		TNT1 A 0 A_JumpIf(Invoker.ADS, 2);
		WPAG R 15 { A_ZoomFactor(5.0, ZOOM_INSTANT); A_StartSound("mauser/scope", CHAN_WEAPON); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); Invoker.ADS = True; }	
		Goto Ready;
		#### D 0 A_JumpIf(!CountInv("Kar98kLoaded"), 2);
		#### A 0;
		WPAG # 15 { A_ZoomFactor(1.0, ZOOM_INSTANT); A_StartSound("mauser/scope", CHAN_WEAPON); A_WeaponReady(WRF_NOFIRE); Invoker.ADS = False; }
		Goto Ready;
	LowerZoom:
		WPAG # 15 { A_ZoomFactor(1.0, ZOOM_INSTANT); A_StartSound("mauser/scope", CHAN_WEAPON); A_WeaponReady(WRF_NOFIRE); Invoker.ADS = False; }
		Goto Deselect;

	Reload:
		TNT1 A 0 A_JumpIfInventory("Kar98kLoaded", 5, "Ready");
		TNT1 A 0 A_JumpIf(CountInv("Kar98kLoaded") > 0 || CountInv("PowerInfiniteAmmo"), "ReloadWork");
		TNT1 A 0;
		Goto ReloadWork_Empty; // No more ammo

	ReloadWork:
		TNT1 A 0 A_JumpIf(CountInv("Ammo792M") > 0 || CountInv("PowerInfiniteAmmo"), 1);
        Goto Ready;
		TNT1 A 0 A_JumpIf(Invoker.ADS, "ReloadADS");
		WPAG QPONM 2;
		Goto ReloadWork_Continue;

	ReloadWork_Empty: // No more ammo
		TNT1 A 0 A_JumpIf(CountInv("Ammo792M") > 0 || CountInv("PowerInfiniteAmmo"), 1);
        Goto Ready;
		TNT1 A 0 A_JumpIf(Invoker.ADS, "ReloadADS_Empty");
		WPAG DEFGHI 2;
	ReloadWork_Continue:
		TNT1 A 0 { Invoker.riflereload = True; }
		WPAG J 2 { If(!CountInv("Kar98kLoaded")) { A_SpawnItemEx("Casing_MauserRifle",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION); } }
		#### KL 2;
		#### # 6 A_StartSound("mauser/open", CHAN_WEAPON);
		#### # 1 A_WeaponOffset(-4,34,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-6,35,WOF_INTERPOLATE);
		#### # 2 A_WeaponOffset(-8,36,WOF_INTERPOLATE);
		#### # 2 A_WeaponOffset(-4,42,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,51,WOF_INTERPOLATE);	
		#### # 1 A_WeaponOffset(4,60,WOF_INTERPOLATE);
		#### # 2 A_WeaponOffset(5,74,WOF_INTERPOLATE);
		#### # 3 A_WeaponOffset(6,76,WOF_INTERPOLATE);
		#### # 5;
	ReloadLoop:
		TNT1 A 0 A_JumpIf(CountInv("Kar98kLoaded") == 5 || CountInv("Ammo792M") < 1 && !CountInv("PowerInfiniteAmmo"), "ReloadFinish");
		WPAG L 1
		{
			A_WeaponOffset(6,80,WOF_INTERPOLATE);
			A_StartSound("mauser/insert", CHAN_WEAPON);
			A_TakeInventory("Ammo792M", 1, TIF_NOTAKEINFINITE);
			A_GiveInventory("Kar98kLoaded", 1);
		}
		#### # 1 A_WeaponOffset(6,84,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(6,87,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(7,90,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(8,92,WOF_INTERPOLATE);
		#### # 8 A_WeaponOffset(8,92,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(9,88,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(8,82,WOF_INTERPOLATE);
		#### # 1 { A_WeaponOffset(7,77,WOF_INTERPOLATE); A_WeaponReady(WRF_NOBOB); }
		TNT1 A 0 A_JumpIf(CountInv("Kar98kLoaded") == 5 || CountInv("Ammo792M") < 1 && !CountInv("PowerInfiniteAmmo"), "ReloadFinish");
		Goto ReloadLoop;
	ReloadFinish:
		WPAG KJMNOPQ 2;
		WPAG A 5 A_StartSound("mauser/shut", CHAN_WEAPON);
		#### # 1 A_WeaponOffset(4,60,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,51,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-4,42,WOF_INTERPOLATE);
		#### # 2 A_WeaponOffset(-8,36,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-6,35,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-4,34,WOF_INTERPOLATE);
		#### # 2 { A_WeaponOffset(-2,33,WOF_INTERPOLATE); Invoker.riflereload = False; }
		TNT1 A 0 A_JumpIf(Invoker.riflelower, "Deselect");
		Goto Ready;

	ReloadADS:
		WPAG A 1 { A_ZoomFactor(1.0, ZOOM_INSTANT); A_StartSound("mauser/scope", CHAN_WEAPON); Invoker.ADS = False; }
		Goto ReloadWork;
	ReloadADS_Empty:
		WPAG A 1 { A_ZoomFactor(1.0, ZOOM_INSTANT); A_StartSound("mauser/scope", CHAN_WEAPON); Invoker.ADS = False; }
		Goto ReloadWork_Empty;
	}
}

Class Kar98kLoaded : Ammo
{
	Default
	{
		Tag "7,92 Ã— 57 mm Mauser";
		+INVENTORY.IGNORESKILL
		Inventory.MaxAmount 5;
		Inventory.Icon "MGUNA0";
	}
}