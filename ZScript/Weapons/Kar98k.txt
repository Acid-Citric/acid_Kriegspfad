Class Kar98k : AxisWeapon
{
	Bool ADS;
	Default
	{
		Weapon.SelectionOrder 2500;

		Weapon.AmmoType "Kar98kLoaded";
		Weapon.AmmoType2 "Ammo792M";
		Inventory.PickupMessage "You got the Kar98k!";
		Tag "Karabiner 98 kurz";
		Weapon.UpSound "rifle/select";
	}
	Override Void AttachToOwner(Actor other)
	{
		Super.AttachToOwner(other);
		other.A_GiveInventory("Kar98kLoaded", random(0,5));
	}
   	Override Bool TryPickup(in out Actor toucher)
    {
		If(toucher)
		{
			Let weap = toucher.FindInventory("Kar98k");
			If(!weap) { Self.AmmoGive2 = 0; }
			Else If(bTOSSED) { Self.AmmoGive2 = random(1,5); }
			Else { Self.AmmoGive2 = random(1,5); }
		}
			Return Super.TryPickup(toucher);
	}
	States
	{
	Spawn:
		WPAP A -1;
		Stop;
	Select:
		WPAG A 1 A_Raise(30);
		Loop;
	Ready:
		WPAG A 0 A_JumpIf(Invoker.ADS == True, 2);
		WPAG A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		Loop;
		#### R 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_NOBOB);
		Loop;	
	Deselect:
		WPAG A 1 A_Lower(30);
		Loop;

	Fire:
		TNT1 A 0 A_JumpIf(!CountInv("Kar98kLoaded"), "Reload");
		TNT1 A 0 A_JumpIf(Invoker.ADS, "FireScope");
		WPAG B 1
		{
			A_GunFlash();
			A_AlertMonsters();
			A_FireBullets(0, 0, 1, random(130,200), "BulletPuff_2");
			A_StartSound("kar98/fire", CHAN_WEAPON);
			A_TakeInventory("Kar98kLoaded",1);
		}
		#### C 1;	
		#### D 2 A_WeaponOffset(0,40,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(0,36,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		#### FGHHII 1;
		#### J 7 { A_StartSound("kar98/rechamber",CHAN_WEAPON); A_SpawnItemEx("Casing_MauserRifle",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION); }	
		#### MMNNOPQA 1;
		Goto Ready;
	Flash:
		WPAF A 1 BRIGHT A_Light2;
		WPAF B 1 BRIGHT A_Light1;
		Goto LightDone;

	FireScope:
		WPAG R 2 BRIGHT
		{
			A_AlertMonsters();
			A_FireBullets(0, 0, 1, random(130,200), "BulletPuff_2");
			A_StartSound("kar98/fire", CHAN_WEAPON);
			A_TakeInventory("Kar98kLoaded",1);
		}
		#### R 4;
		#### R 3 A_StartSound("kar98/rechamber",CHAN_WEAPON);
		#### R 18;
		Goto Ready;
	
	Empty:
		WPAG D 1
		{
			A_OverlayScale(PSP_WEAPON, 1.00, 1.00, WOF_RELATIVE);
			A_WeaponOffset(0,32,WOF_INTERPOLATE);
			//A_WeaponReady(WRF_NOFIRE);
		}
		Goto Ready;

	AltFire:
		TNT1 A 0 A_JumpIf(Invoker.ADS, 2);
		WPAG R 15 { A_ZoomFactor(5.0, ZOOM_INSTANT); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); Invoker.ADS = True; }	
		Goto Ready;
		WPAG A 15 { A_ZoomFactor(1.0, ZOOM_INSTANT); A_WeaponReady(WRF_NOFIRE); Invoker.ADS = False; }
		Goto Ready;

	Reload:
		TNT1 A 0 A_JumpIfInventory("Kar98kLoaded", 5, "Ready");
		TNT1 A 0 A_JumpIf(CountInv("Kar98kLoaded") > 0 || CountInv("PowerInfiniteAmmo"), "ReloadWork");
		TNT1 A 0;
		Goto ReloadWork_Empty; // No more ammo

	ReloadWork:
		TNT1 A 0 A_JumpIf(CountInv("Ammo792M") > 0 || CountInv("PowerInfiniteAmmo"), 1);
        Goto Ready;
		TNT1 A 0 A_JumpIf(Invoker.ADS, "ReloadADS");
		WPAG QPONM 2;
		Goto ReloadWork_Continue;

	ReloadWork_Empty: // No more ammo
		TNT1 A 0 A_JumpIf(Invoker.ADS, "ReloadADS_Empty");
		WPAG DEFGHI 2;
	ReloadWork_Continue:
		WPAG JKL 2;
		#### # 6 A_StartSound("kar98/reload", CHAN_WEAPON);
		#### # 1 A_WeaponOffset(-4,34,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-6,35,WOF_INTERPOLATE);
		#### # 2 A_WeaponOffset(-8,36,WOF_INTERPOLATE);
		#### # 2 A_WeaponOffset(-4,42,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,51,WOF_INTERPOLATE);	
		#### # 1 A_WeaponOffset(4,60,WOF_INTERPOLATE);
		#### # 2 A_WeaponOffset(5,74,WOF_INTERPOLATE);
		#### # 3 A_WeaponOffset(6,76,WOF_INTERPOLATE);
		#### # 5;
	//ReloadLoop:
		#### # 1 A_WeaponOffset(6,80,WOF_INTERPOLATE); //A_StartSound("Enfield/load", CHAN_WEAPON);
		#### # 1 A_WeaponOffset(6,84,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(6,87,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(7,90,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(8,92,WOF_INTERPOLATE);
		#### # 8 A_WeaponOffset(8,92,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(9,88,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(8,82,WOF_INTERPOLATE);
		#### # 1 { A_WeaponOffset(7,77,WOF_INTERPOLATE); A_WeaponReady(WRF_NOBOB|WRF_NOFIRE); }
	ReloadLoop:
		TNT1 A 0
		{
           	While(CountInv("Ammo792M") > 0 && CountInv("Kar98kLoaded") < 5 || CountInv("PowerInfiniteAmmo") && CountInv("Kar98kLoaded") < 5)
			{
				A_TakeInventory("Ammo792M", 1, TIF_NOTAKEINFINITE);
				A_GiveInventory("Kar98kLoaded", 1);
			}
		}
		TNT1 A 0;
	ReloadFinish:
		WPAG KJMNOPQ 2;
		WPAG A 5; // A_StartSound("mauser/shut", CHAN_WEAPON);
		#### # 1 A_WeaponOffset(4,60,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,51,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-4,42,WOF_INTERPOLATE);
		#### # 2 A_WeaponOffset(-8,36,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-6,35,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-4,34,WOF_INTERPOLATE);
		#### # 2 A_WeaponOffset(-2,33,WOF_INTERPOLATE);
		Goto Ready;

	ReloadADS:
		WPAG A 1 { A_ZoomFactor(1.0, ZOOM_INSTANT); Invoker.ADS = False; }
		Goto ReloadWork;
	ReloadADS_Empty:
		WPAG A 1 { A_ZoomFactor(1.0, ZOOM_INSTANT); Invoker.ADS = False; }
		Goto ReloadWork_Empty;
	}
}

Class Kar98kLoaded : Ammo
{
	Default
	{
		Tag "7,92 Ã— 57 mm Mauser";
		+INVENTORY.IGNORESKILL
		Inventory.MaxAmount 5;
		Inventory.Icon "MGUNA0";
	}
}
