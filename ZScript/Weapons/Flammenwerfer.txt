Class Flammenwerfer : AxisWeapon replaces PlasmaRifle
{
	Bool selected;
	Int counter;
	Default
	{
		Weapon.SelectionOrder 100;

		Weapon.AmmoUse 1;
		Weapon.AmmoType "AmmoFuel";
		Inventory.PickupMessage "You got the Flammenwerfer!";
		+WEAPON.AMMO_OPTIONAL
		Tag "Flammenwerfer";
		Weapon.UpSound "pistol/select";
	}
   	Override Bool TryPickup(in out Actor toucher)
    {
		If(toucher)
		{
			Let weap = toucher.FindInventory("Flammenwerfer");
			If(!weap) { Self.AmmoGive1 = random(50,150); }
			Else { Self.AmmoGive1 = random(50,150); }
		}
			Return Super.TryPickup(toucher);
	}
	States
	{
	Spawn:
		WP6P A -1;
		Stop;
	Select:
		WP6R D 0 A_JumpIf(Invoker.selected == False, 2);
		WP6H A 0;
		#### # 1 A_Raise(30);
		Loop;
	Ready:
		TNT1 A 0 A_JumpIf(!CountInv("AmmoFuel"), "Empty");
		TNT1 A 0 A_JumpIf(Invoker.selected == True, "ReadyLoop");
		WP6R D 20;
		#### CBA 2;
	ReadyLoop:
		WP6H ABCD 2
		{
			Invoker.selected = True;
			return A_JumpIf(!CountInv("AmmoFuel"), "Empty");
		}
		WP6G ABCD 2
		{
			A_WeaponReady();
			return A_JumpIf(!CountInv("AmmoFuel"), "Empty");
		}
		Goto ReadyLoop+5;
	Empty:
		TNT1 A 0 A_JumpIf(CountInv("AmmoFuel"), "ReadyLoop");
		WP6H A 1 A_WeaponReady(WRF_NOFIRE);
		Loop;		

	Deselect:
		WP6H A 1 A_Lower(30);
		Loop;

	Fire:
		WP6F A 0 A_JumpIfInventory("AmmoFuel", 1, 1);
		Goto Empty;
		#### B 1 BRIGHT
		{
			A_GunFlash();
			A_AlertMonsters();
			A_FireProjectile("FmWShot", 0, False, 0, 5);
			A_WeaponOffset(random(1,2),random(33,34),WOF_INTERPOLATE);
			
			If(Invoker.counter == 5) { Invoker.counter = 0; A_TakeInventory("AmmoFuel", 1, TIF_NOTAKEINFINITE); }
			Else { Invoker.counter++; }
		}
		#### A 0 A_ReFire();
		#### B 3 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		Goto ReadyLoop;
	Flash:
		TNT1 A 1 BRIGHT A_Light2;
		TNT1 A 1 BRIGHT A_Light1;
		Goto LightDone;
	}
}

Class FmWShot : FastProjectile
{
	Default
	{
		//SeeSound "weapons/rocklf";

		+DONTSPLASH
		+NOEXTREMEDEATH
		+RIPPER
		+BLOODLESSIMPACT
		Speed 25;
		Radius 16;
		Mass 10;
		DamageFunction random(4,6);
		DamageType "Fire";
		PROJECTILE;
		RenderStyle "Add";
		Scale 0.2;
		Alpha 0.5;
		
		ProjectileKickback 20;
	}
	States
	{
	Spawn:
		FIR3 A 0 NoDelay A_Jump(128,"SpawnMirrored");
		FIR3 AABBCCDDEEFFGGHHIIJJKKLLMMNNOOPP 1 BRIGHT { A_SetScale(Scale.X + 0.01); A_Explode(5,64,0); }
		Stop;
	SpawnMirrored:
		FIR3 AABBCCDDEEFFGGHHIIJJKKLLMMNNOOPP 1 BRIGHT { A_SetScale(Scale.X - 0.01); A_Explode(5,64,0); }
		Stop;
	}
}
