Class STEN : AxisWeapon
{
	Bool STENSequence;
	Default
	{
		Weapon.SelectionOrder 700;

		Weapon.AmmoType "STENLoaded";
		Weapon.AmmoType2 "Ammo9mm";
		Inventory.PickupMessage "You got the STEN Mk II!";
		Tag "$TAG_WEAPON_STENMK2";
		Weapon.UpSound "sten/select";
	}
	Override Void AttachToOwner(Actor other)
	{
		Super.AttachToOwner(other);
		If(bTOSSED) { other.A_GiveInventory("STENLoaded", random(1,16)); }
		Else { other.A_GiveInventory("STENLoaded", random(1,32)); }
	}
   	Override Bool TryPickup(in out Actor toucher)
    {
		If(toucher)
		{
			Let weap = toucher.FindInventory("STEN");
			If(!weap) { Self.AmmoGive2 = 0; }
			Else If(bTOSSED) { Self.AmmoGive2 = random(1,32); }
			Else { Self.AmmoGive2 = random(1,32); }
		}
			Return Super.TryPickup(toucher);
	}
	States
	{
	Spawn:
		WP9P B -1;
		Stop;
	Select:
		WP9U A 1 A_Raise(12);
		Loop;
	Ready:
		WP9U A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		Loop;
	Deselect:
		WP9U A 1 A_Lower(12);
		Loop;

	Fire:
		TNT1 A 0 A_JumpIf(!CountInv("STENLoaded"), "Reload");
		WP9U B 1
		{
			A_FireBullets(1.5, 1.5, 1, random(9,14), "BulletPuff_2", FBF_NORANDOMPUFFZ);
			A_StartSound("sten/fire_silenced", CHAN_WEAPON, CHANF_DEFAULT, 1.0);
			A_AlertMonsters(384);
			A_TakeInventory("STENLoaded",1);
			A_SpawnItemEx("Casing_9MM",12,-20,32*GetCrouchFactor(AAPTR_Default),8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION);
		}
		#### A 0 A_JumpIf(Invoker.STENSequence == True, 2);
		#### # 1 A_WeaponOffset(0,38,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,40,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,36,WOF_INTERPOLATE);
		#### # 0
		{
			If(Invoker.STENSequence == True) { Invoker.STENSequence = False; }
			Else { Invoker.STENSequence = True; }
		}
		#### # 0 A_ReFire();
		Goto Ready;
	Reload:
		TNT1 A 0 A_JumpIfInventory("STENLoaded", 32, 2);
		TNT1 A 0 A_JumpIf(CountInv("Ammo9mm") > 0 || CountInv("PowerInfiniteAmmo"), "ReloadWork");
		TNT1 A 0;
		Goto Ready;
	ReloadWork:
		WP9U D 1
		{
			A_WeaponOffset(0,38,WOF_INTERPOLATE);
			A_StartSound("sten/reload", CHAN_WEAPON);
		}
		#### # 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
		#### F 1 A_WeaponOffset(0,56,WOF_INTERPOLATE);
		#### F 1 A_WeaponOffset(-2,60,WOF_INTERPOLATE);
		#### F 1 A_WeaponOffset(-4,72,WOF_INTERPOLATE);
		#### F 1 A_WeaponOffset(-7,80,WOF_INTERPOLATE);	
		#### F 1 A_WeaponOffset(-10,82,WOF_INTERPOLATE);
		#### F 3 A_WeaponOffset(-11,84,WOF_INTERPOLATE);
	ReloadLoop:
		TNT1 A 0
		{
           	While(CountInv("Ammo9mm") > 0 && CountInv("STENLoaded") < 32 || CountInv("PowerInfiniteAmmo") && CountInv("STENLoaded") < 32)
			{
				A_TakeInventory("Ammo9mm", 1, TIF_NOTAKEINFINITE);
				A_GiveInventory("STENLoaded", 1);
			}
		}
		TNT1 A 0;
	ReloadFinish:
		WP9U D 1 A_WeaponOffset(-10,84,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(-10,90,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(-10,98,WOF_INTERPOLATE);	
		#### D 1 A_WeaponOffset(-10,110,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(-10,98,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(-10,88,WOF_INTERPOLATE);	
		#### D 1 A_WeaponOffset(-10,78,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(-10,74,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(-10,70,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(-9,68,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(-8,60,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-7,54,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-5,50,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-5,48,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-4,44,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-3,42,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-2,38,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-1,36,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-0,34,WOF_INTERPOLATE);
		#### A 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		Goto Ready;
	}
}

Class STENLoaded : Ammo
{
	Default
	{
		+INVENTORY.IGNORESKILL
		Inventory.MaxAmount 32;
	}
}
