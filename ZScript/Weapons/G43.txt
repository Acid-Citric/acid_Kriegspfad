Class G43 : AxisWeapon
{
	Bool RGrenade, wmod;
	Default
	{
		Weapon.SelectionOrder 2500;
		Weapon.SlotNumber 5;
		Weapon.AmmoUse 1;
		Weapon.AmmoType "G43Loaded";
		Weapon.AmmoType2 "Ammo792M";
		Inventory.PickupMessage "You got the Gewehr 43!";
		Tag "$TAG_WEAPON_GEWEHR43";
		Weapon.UpSound "g43/select";
	}
	Override Void AttachToOwner(Actor other)
	{
		Super.AttachToOwner(other);
		other.A_GiveInventory("G43Loaded", random(1, bTOSSED ? 5 : 10));
	}
   	Override Bool TryPickup(in out Actor toucher)
    {
		If(toucher)
		{
			Let weap = toucher.FindInventory("G43");
			If(!weap) { Self.AmmoGive2 = 0; }
			Else { Self.AmmoGive2 = random(1,10); }
		}
			Return Super.TryPickup(toucher);
	}

	States
	{
	Spawn:
		TNT1 A 0 NoDelay { For(int i; i<2; i++) { A_SpawnItemEx("AmmoRifleGrenade",Random(-8,8),Random(-8,8),failchance: 128); } }
		WP4P A -1;
		Stop;
	Select:
		WP4G A 1 A_Raise(12);
		Loop;
	Ready:
		WP4G D 0 A_JumpIf(Invoker.RGrenade == True, 2);
		#### A 0;
		WP4G # 1 A_WeaponReady(WRF_ALLOWRELOAD);
		Loop;
	Deselect:
		WP4G A 1 A_Lower(12);
		Loop;

	Fire:
		TNT1 A 0 A_JumpIf(!CountInv(Invoker.Ammo1.GetClass()), "Reload");
		WP4G B 1
		{
			A_AlertMonsters();
			If(Invoker.RGrenade == False)
			{
				A_GunFlash();
/*
				FTranslatedLineTarget t;
				double ang = angle + Random2() * (5.625 / 256);
				double pitch = AimLineAttack(ang, 64, null, 0., ALF_CHECK3D);
				LineAttack(ang, 8192, BulletSlope() - pitch, random(1,3) * random(130,200), 'Hitscan', "BulletPuff_2", LAF_NORANDOMPUFFZ, t);
*/
				A_RailAttack(random(1,3) * random(130,200), 0, True, "", "", RGF_SILENT, 0, "BulletPuff_2", 0, BulletSlope() - Pitch, 8192, 1);
				A_StartSound("g43/fire", CHAN_WEAPON);
			}
			Else 
			{ 
				A_FireProjectile("RifleGrenade", 0, False);
				A_TakeInventory("AmmoRifleGrenade", 1, TIF_NOTAKEINFINITE);
				A_StartSound("g43/grenadeshot", CHAN_WEAPON);
			}
			A_FireProjectile("Casing_Spawner_MauserRifle", 0, False, 1, 1, FPF_NOAUTOAIM);
		}
		#### C 1 { A_WeaponOffset(0,40,WOF_INTERPOLATE); Invoker.RGrenade = False; }
		#### A 1 A_WeaponOffset(0,36,WOF_INTERPOLATE);
		#### A 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		#### A 9 A_WeaponReady(WRF_NOFIRE);
		Goto Ready;
	Flash:
		WP4F A 1 BRIGHT A_Light2;
		WP4F B 1 BRIGHT A_Light1;
		Goto LightDone;

	AltFire:
		TNT1 A 0 A_JumpIf(!CountInv("AmmoRifleGrenade"), "Ready");
		TNT1 A 0 { Invoker.wmod = True; }
		Goto ReloadWork;

	Reload:
		TNT1 A 0 A_JumpIf(Invoker.Ammo1.Amount == Invoker.Ammo1.MaxAmount, 2);
		TNT1 A 0 A_JumpIf(Invoker.Ammo2.Amount > 0, "ReloadWork");
		TNT1 A 0;
		Goto Ready;
	ReloadWork:
		WP4G A 1
		{
			A_WeaponOffset(0,35,WOF_INTERPOLATE);
			If(Invoker.wmod == False) { A_StartSound("g43/reload", CHAN_WEAPON); }
			Else { frame = 3; }
		}
		#### E 1 A_WeaponOffset(-2,38,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-4,44,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-6,52,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-7,57,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-8,62,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-9,67,WOF_INTERPOLATE);
		#### E 0 A_JumpIf(Health < 25, 1);
		Goto ReloadLoop2;
		#### E 1 A_WeaponOffset(-10,72,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-11,77,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-11,80,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-12,82,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-12,84,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-13,86,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-13,87,WOF_INTERPOLATE);
		#### E 2 A_WeaponOffset(-14,88,WOF_INTERPOLATE);
		#### E 3 A_WeaponOffset(-14,89,WOF_INTERPOLATE);
		#### E 0
		{
			If(Invoker.wmod == True)
			{
				Invoker.RGrenade =! Invoker.RGrenade;
				A_StartSound("g43/grenadeload", CHAN_WEAPON);
			}
		}
		#### # 0 A_JumpIf(Invoker.wmod && Invoker.RGrenade || Invoker.wmod, 3);
	ReloadLoop:
		#### # 0 A_MagazineReload("G43Loaded");
	ReloadFinish:
		#### F 2 A_WeaponOffset(-10,93,WOF_INTERPOLATE);
		#### F 1 A_WeaponOffset(-6,100,WOF_INTERPOLATE);
		#### F 1 A_WeaponOffset(-7,90,WOF_INTERPOLATE);	
		#### F 1 A_WeaponOffset(-6,80,WOF_INTERPOLATE);
		#### F 1 A_WeaponOffset(-4,70,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-2,60,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(0,50,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(0,40,WOF_INTERPOLATE);
		#### A 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		#### A 0 { If(Invoker.wmod) { Invoker.wmod = False; } }
		Goto Ready;

	ReloadLoop2:
		#### E 1 A_WeaponOffset(-10,72,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-11,77,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-11,80,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-12,82,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-12,84,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-13,86,WOF_INTERPOLATE);
		#### E 1 A_WeaponOffset(-13,87,WOF_INTERPOLATE);
		#### E 2 A_WeaponOffset(-14,88,WOF_INTERPOLATE);
		#### E 0
		{
			If(Invoker.wmod == True)
			{
				Invoker.RGrenade =! Invoker.RGrenade;
				A_StartSound("g43/grenadeload", CHAN_WEAPON);
			}
		}
		#### # 0 A_JumpIf(Invoker.wmod && Invoker.RGrenade || Invoker.wmod, "ReloadFinish");
		#### E 0 A_JumpIf(Invoker.Ammo1.Amount == Invoker.Ammo1.MaxAmount || Invoker.Ammo2.Amount == 0, "ReloadFinish");
		#### E 0
		{
			A_WeaponOffset(-14,89,WOF_INTERPOLATE);
			
			Int stripperclip = 5;
			Bool JustOne = (Invoker.Ammo2.Amount < stripperclip || Invoker.Ammo1.Amount > (Invoker.Ammo1.MaxAmount - stripperclip));
			A_HandReload(Invoker.AmmoType1, JustOne ? 1 : stripperclip, JustOne ? 1 : stripperclip);
			A_StartSound(JustOne ? "mauser/insert" : "shotgun/pump", CHAN_WEAPON);
			A_SetTics(JustOne ? 2 : 10);
		}
		#### E 2 { A_WeaponOffset(-10,93,WOF_INTERPOLATE); A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY); }
		Goto ReloadLoop2;
	}
}

Class G43Loaded : Ammo
{
	Default
	{
		+INVENTORY.IGNORESKILL
		Inventory.MaxAmount 10;
	}
}

Class RifleGrenade : Rocket
{
	Default
	{
		Health 10;
		+MISSILE
		-ROCKETTRAIL
		+SHOOTABLE
		Speed 150;
		Gravity 0.4;
		Scale 1.2;
	}
	States
	{
	Spawn:
		//SGRN A
		[RG] B 1 BRIGHT;
		Loop;
	Death:
		TNT1 A 10
		{
			A_Explode(192, 256, XF_HURTSOURCE, True, 128);
			A_SpawnItemEx("fx_realisticexplosion_s",0,0,0); //gta2_expl_explosion
		}
		Stop;
	}
}
