Class Luger : AxisWeapon replaces Pistol
{
	Default
	{
		Weapon.SelectionOrder 2100;
		Weapon.SlotNumber 2;
		Weapon.AmmoUse 1;
		Weapon.AmmoType "LugerLoaded";
		Weapon.AmmoType2 "Ammo9mm";
		Inventory.PickupMessage "You got the Luger P08!";
		Tag "$TAG_WEAPON_LUGERP08";
		Weapon.UpSound "luger/select";
		Weapon.Kickback 60;
	}
	Override Void AttachToOwner(Actor other)
	{
		Super.AttachToOwner(other);
		other.A_GiveInventory("LugerLoaded", random(1, bTOSSED ? 4 : 8));
	}	
   	Override Bool TryPickup(in out Actor toucher)  
    {
		If(toucher)
		{
			Let weap = toucher.FindInventory("Luger");
			If(!weap) { Self.AmmoGive2 = 0; }
			Else { Self.AmmoGive2 = random(1,8); }
		}
			Return Super.TryPickup(toucher);
	}

	States
	{
	Spawn:
		WP1P A -1;
		Stop;
	Select:
		WP1E C 0 A_JumpIf(!CountInv(Invoker.Ammo1.GetClass()), 2);
		WP1G F 0;
		#### # 1 A_Raise(12);
		Loop;
	Ready:
		WP1E A 0 A_JumpIf(!CountInv(Invoker.Ammo1.GetClass()), 3);
		WP1G FG 2;
		WP1G A 0 A_JumpIf(CountInv(Invoker.Ammo1.GetClass()), 3);
		WP1E CD 2;
	ReadyLoop: // only for the 'take all' / 'give all' commands
		WP1E A 0 A_JumpIf(!CountInv(Invoker.Ammo1.GetClass()), 2);
		WP1G A 0;
		#### # 1 A_WeaponReady(WRF_ALLOWRELOAD);
		Loop; //Wait;
	Deselect:
		WP1E C 0 A_JumpIf(!CountInv(Invoker.Ammo1.GetClass()), 2);
		WP1G F 0;
		#### # 1 A_Lower(12);
		Loop;

	Fire:
		TNT1 A 0 A_JumpIf(!CountInv(Invoker.Ammo1.GetClass()), "Reload");
		WP1G B 1
		{
			A_GunFlash();
			A_AlertMonsters();
			A_FireBullets(1, 1, -1, random(3,17), "BulletPuff_2", FBF_USEAMMO|FBF_NORANDOMPUFFZ);
			A_StartSound("luger/fire", CHAN_WEAPON, CHANF_DEFAULT, 1);

			//Int SX_zofs = 32*GetCrouchFactor(AAPTR_Default);
			//A_SpawnItemEx("Casing_9MM",cos(pitch)*12,-20,(SX_zofs)-(sin(pitch)*(SX_zofs)),cos(pitch)*8,random(-2,2),-sin(pitch)*random(0,4),random(-55,-80),SXF_TRANSFERPITCH);
			A_FireProjectile("Casing_Spawner_9MM", 0, False, 1, 1, FPF_NOAUTOAIM);
		}
		#### C 1 A_WeaponOffset(0,36,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(0,41,WOF_INTERPOLATE);
		#### A 0 A_JumpIf(!CountInv(Invoker.Ammo1.GetClass()), 5);
		#### E 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		#### F 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		#### G 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		#### A 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		Goto Ready+6;
		WP1E B 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		#### C 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		#### A 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		Goto Ready+6;
		
	Flash:
		WP1F A 1 BRIGHT A_Light1();
		Goto LightDone;
	Reload:
		TNT1 A 0 A_JumpIf(Invoker.Ammo1.Amount == Invoker.Ammo1.MaxAmount, 2);
		TNT1 A 0 A_JumpIf(Invoker.Ammo2.Amount > 0, "ReloadWork");
		TNT1 A 0;
		Goto Ready+6;

	ReloadWork:
		WP1G E 1 A_JumpIf(!Invoker.Ammo1.GetClass(), 2);
		#### D 1;
		#### C 1;
		#### H 1
		{
			A_WeaponOffset(0,35,WOF_INTERPOLATE);
			If(!Invoker.Ammo1.GetClass()) { A_StartSound("luger/reload", CHAN_WEAPON); }
			Else { A_StartSound("luger/reload", CHAN_WEAPON); }
		}
		#### # 1 A_WeaponOffset(0,38,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,52,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,62,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,72,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,82,WOF_INTERPOLATE);	
		TNT1 A 11;
	ReloadLoop:
		TNT1 A 0 A_MagazineReload(Invoker.AmmoType1);
	ReloadFinish:
		WP1G F 1 A_WeaponOffset(0,82,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,72,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,62,WOF_INTERPOLATE);
		#### G 1 A_WeaponOffset(0,52,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,38,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		Goto Ready+6;
	}
}

Class LugerLoaded : Ammo
{
	Default
	{
		+INVENTORY.IGNORESKILL
		Inventory.MaxAmount 8;
	}
}
