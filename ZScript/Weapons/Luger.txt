Class Luger : AxisWeapon replaces Pistol
{
	Default
	{
		Weapon.SelectionOrder 2100;

		Weapon.AmmoType "LugerLoaded";
		Weapon.AmmoType2 "Ammo9mm";
		Inventory.PickupMessage "You got the Luger P08!";
		Tag "Luger P08";
		Weapon.UpSound "pistol/select";
		Weapon.Kickback 60;
	}
	Override Void AttachToOwner(Actor other)
	{
		Super.AttachToOwner(other);
		other.A_GiveInventory("LugerLoaded", random(0,8));
	}
   	Override Bool TryPickup(in out Actor toucher)  
    {
		If(toucher)
		{
			Let weap = toucher.FindInventory("Luger");
			If(!weap) { Self.AmmoGive2 = 0; }
			Else If(bTOSSED) { Self.AmmoGive2 = random(1,8); }
			Else { Self.AmmoGive2 = random(1,8); }
		}
			Return Super.TryPickup(toucher);
	}
	States
	{
	Spawn:
		WP1P A -1;
		Stop;
	Select:
		WP1E C 0 A_JumpIf(!CountInv("LugerLoaded"), 2);
		WP1G F 0;
		#### # 1 A_Raise(30);
		Loop;
	Ready:
		WP1E A 0 A_JumpIf(!CountInv("LugerLoaded"), 2);
		WP1G A 0;
		#### A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		Loop;
	Deselect:
		WP1E C 0 A_JumpIf(!CountInv("LugerLoaded"), 2);
		WP1G F 0;
		#### # 1 A_Lower(30);
		Loop;

	Fire:
		TNT1 A 0 A_JumpIf(!CountInv("LugerLoaded"), "Reload");
		WP1G B 2
		{
			A_GunFlash();
			A_AlertMonsters();
			A_FireBullets(1, 1, 1, random(3,17), "BulletPuff_2");
			A_StartSound("luger/fire", CHAN_WEAPON, CHANF_DEFAULT, 0.5);
			A_TakeInventory("LugerLoaded",1);
			A_SpawnItemEx("Casing_9MM",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION);
		}
		#### C 1 A_WeaponOffset(0,36,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(0,41,WOF_INTERPOLATE);
		#### A 0 A_JumpIf(!CountInv("LugerLoaded"), 5);
		#### E 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		#### F 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		#### G 1 A_WeaponOffset(0,31,WOF_INTERPOLATE);
		#### A 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		Goto Ready;
		WP1E B 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		#### C 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(0,31,WOF_INTERPOLATE);
		#### A 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		Goto Ready;
		
	Flash:
		WP1F A 2 BRIGHT A_Light1;
		Goto LightDone;
	Reload:
		TNT1 A 0 A_JumpIfInventory("LugerLoaded", 8, 2);
		TNT1 A 0 A_JumpIf(CountInv("Ammo9mm") > 0 || CountInv("PowerInfiniteAmmo"), "ReloadWork");
		TNT1 A 0;
		Goto Ready;

	ReloadWork:
		WP1E C 0 A_JumpIf(!CountInv("LugerLoaded"), 2);
		WP1G F 0;
		#### # 1
		{
			A_WeaponOffset(0,35,WOF_INTERPOLATE);
			If(!CountInv("LugerLoaded")) { A_StartSound("luger/reload", CHAN_WEAPON); }
			Else { A_StartSound("luger/reload", CHAN_WEAPON); }
		}
		#### # 1 A_WeaponOffset(0,38,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,52,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,62,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,72,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,82,WOF_INTERPOLATE);
		TNT1 A 11;
	ReloadLoop:
		TNT1 A 0
		{
           	While(CountInv("Ammo9mm") > 0 && CountInv("LugerLoaded") < 8 || CountInv("PowerInfiniteAmmo") && CountInv("LugerLoaded") < 8)
			{
				A_TakeInventory("Ammo9mm", 1, TIF_NOTAKEINFINITE);
				A_GiveInventory("LugerLoaded", 1);
			}
		}
		TNT1 A 0;
	ReloadFinish:
		WP1G F 1 A_WeaponOffset(0,82,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,72,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,62,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,52,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,38,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		Goto Ready;
	}
}

Class LugerLoaded : Ammo
{
	Default
	{
		Tag "9 Ã— 19 mm Parabellum";
		+INVENTORY.IGNORESKILL
		Inventory.MaxAmount 8;
		Inventory.Icon "PISTA0";
	}
}
