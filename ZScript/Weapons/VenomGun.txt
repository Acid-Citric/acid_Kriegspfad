Class VenomGun : AxisWeapon replaces BFG9000
{
	Default
	{
		Weapon.SelectionOrder 2800;

		Weapon.AmmoUse 1;
		Weapon.AmmoType "Ammo127M";
		Inventory.PickupMessage "You got the Venom Gun MK1!";
		+WEAPON.AMMO_OPTIONAL
		Tag "GP-VG MK1 Venom Gun";
		Weapon.UpSound "pistol/select";
	}
   	Override Bool TryPickup(in out Actor toucher)
    {
		If(toucher)
		{
			Let weap = toucher.FindInventory("VenomGun");
			If(!weap) { Self.AmmoGive1 = random(0,500); }
			Else { Self.AmmoGive1 = random(50,500); }
		}
			Return Super.TryPickup(toucher);
	}
	States
	{
	Spawn:
		WP5P A -1;
		Stop;
	Select:
		WP5G A 1 A_Raise(30);
		Loop;
	Ready:
		WP5G A 1 A_WeaponReady();
		Loop;

	Deselect:
		WP5G A 1 A_Lower(30);
		Loop;

	Fire:
		WP5G A 0 A_JumpIfInventory("Ammo127M",1,1);
		Goto Ready;
		#### A 0 A_StartSound("chaingun/start", CHAN_WEAPON);
		#### A 1 A_StartSound("chaingun/loop", CHAN_WEAPON, 1, True);
		#### AAABBBCCD 1;
		#### D 1 A_ReFire();
	FireFinish:
		WP5G D 0 { A_WeaponOffset(0,32,WOF_INTERPOLATE); A_StartSound("chaingun/stop", CHAN_WEAPON); }
		#### DABCDABCDAABBBCCCCDDDDD 1 A_WeaponReady();
		Goto Ready;
	Hold:
		#### A 0 A_JumpIfInventory("Ammo127M",1,1);
		Goto FireFinish;
		WP5F A 1
		{
			A_GunFlash();
			A_AlertMonsters();
			A_FireBullets(5, 5, 1, random(75,130), "ExplosivePuff", FBF_USEAMMO);
			A_StartSound("weapons/pistol", CHAN_WEAPON);
			A_WeaponOffset(0,34,WOF_INTERPOLATE);
			A_SpawnItemEx("Casing_MauserRifle",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION);
		}
		#### BCD 1;
		#### A 0 A_JumpIfInventory("Ammo127M",1,1);
		Goto FireFinish;
		#### A 0 A_ReFire();
		Goto FireFinish;
	Flash:
		TNT1 A 1 BRIGHT A_Light2;
		TNT1 A 3 BRIGHT A_Light1;
		Goto LightDone;
	}
}

Class ExplosivePuff : BulletPuff
{
	Default
    {
	    +NOEXTREMEDEATH
		Scale 0.2;
	}
	States
	{
	Spawn:
		MISL B 8 Bright;
		MISL C 6 Bright;
		MISL D 4 Bright;
		Stop;		
	}
}
