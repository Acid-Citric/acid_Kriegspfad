Class MP40 : AxisWeapon
{
	Bool MP40Sequence;
	Default
	{
		Weapon.SelectionOrder 700;
		Weapon.SlotNumber 4;
		Weapon.AmmoUse 1;
		Weapon.AmmoType "MP40Loaded_1";
		Weapon.AmmoType2 "Ammo9mm";
		Inventory.PickupMessage "You got the MaschinenPistole 40/1!";
		Tag "$TAG_WEAPON_MP401";
		Weapon.UpSound "mp40/select";
	}
	Override Void AttachToOwner(Actor other)
	{
		Super.AttachToOwner(other);
		other.A_GiveInventory("MP40Loaded_1", random(1,32));
		other.A_GiveInventory("MP40Loaded_2", random(1,32));
	}
   	Override Bool TryPickup(in out Actor toucher)
    {
		If(toucher)
		{
			Let weap = toucher.FindInventory("MP40");
			If(!weap) { Self.AmmoGive2 = 0; }
			Else { Self.AmmoGive2 = random(1,64); }
		}
			Return Super.TryPickup(toucher);
	}

	States
	{
	Spawn:
		WP3P A -1;
		Stop;
	Select:
		WP3G A 1 A_Raise(12);
		Loop;
	Ready:
		WP3G A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		Loop;
	Deselect:
		WP3G A 1 A_Lower(12);
		Loop;

	Fire:
		TNT1 A 0 A_JumpIf(!CountInv(Invoker.AmmoType1), "AltFire");
		WP3G B 1
		{
			A_GunFlash();
			A_AlertMonsters();
			A_FireBullets(1.4, 1.4, 1, random(9,14), "BulletPuff_2", FBF_USEAMMO|FBF_NORANDOMPUFFZ);
			A_StartSound("mp40/fire", CHAN_WEAPON);
			A_FireProjectile("Casing_Spawner_9MM", 0, False, 0, 0, FPF_NOAUTOAIM);
		}
		#### A 3
		{
			If(Invoker.MP40Sequence) { A_SetTics(4); Invoker.MP40Sequence = False; }
			Else { Invoker.MP40Sequence = True; }
			A_WeaponOffset(0,random(33,34),WOF_INTERPOLATE);
		}
		#### # 0 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		#### # 0 A_ReFire();
		Goto Ready;
	Flash:
		WP3F A 1 BRIGHT A_Light1;
		Goto LightDone;

	AltFire:
		TNT1 A 0 A_JumpIf(!CountInv("MP40Loaded_1") && !CountInv("MP40Loaded_2"), "Reload");	
		TNT1 A 0 A_JumpIf(Invoker.AmmoType1 == "MP40Loaded_1" && !CountInv("MP40Loaded_2") || Invoker.AmmoType1 == "MP40Loaded_2" && !CountInv("MP40Loaded_1"), "Ready");
		WP3G A 1 { A_WeaponOffset(0,35,WOF_INTERPOLATE); A_StartSound("mp40/magswitch", CHAN_WEAPON); }
		#### # 1 A_WeaponOffset(-1,38,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-2,44,WOF_INTERPOLATE);
		#### # 5
		{
			A_WeaponOffset(-3,52,WOF_INTERPOLATE);

			If(Invoker.AmmoType1 == "MP40Loaded_1") { SetAmmoType1(True, "MP40Loaded_2"); }
			Else If(Invoker.AmmoType1 == "MP40Loaded_2") { SetAmmoType1(True, "MP40Loaded_1"); }
		}
		#### # 1 A_WeaponOffset(-4,64,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-4,67,WOF_INTERPOLATE);
		#### # 2 A_WeaponOffset(-4,71,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-4,67,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-4,64,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(-3,52,WOF_INTERPOLATE);	
		#### # 1 A_WeaponOffset(-2,44,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-1,38,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		Goto Ready;

	Reload:
		TNT1 A 0 A_JumpIf(CountInv("MP40Loaded_1") == Invoker.Ammo1.MaxAmount && CountInv("MP40Loaded_2") == Invoker.Ammo1.MaxAmount, 2);
		TNT1 A 0 A_JumpIf(Invoker.Ammo2.Amount > 0, "ReloadWork");
		TNT1 A 0;
		Goto Ready;
	ReloadWork:
		TNT1 A 0 A_JumpIf(Invoker.Ammo2.Amount > 0, 1);
        Goto Ready;
		WP3G C 1
		{
			A_WeaponOffset(0,35,WOF_INTERPOLATE);
			A_StartSound("mp40/reload", CHAN_WEAPON);		
		}
		#### # 1 A_WeaponOffset(0,38,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(0,52,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-1,54,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-2,56,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-3,58,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-4,58,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-4,57,WOF_INTERPOLATE);
	ReloadMags:
		#### # 1 A_WeaponOffset(-3,54,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-3,51,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-3,53,WOF_INTERPOLATE);
		#### # 1 A_WeaponOffset(-3,55,WOF_INTERPOLATE);
		#### # 6 A_WeaponOffset(-3,56,WOF_INTERPOLATE);
	ReloadLoop:
		#### A 0
		{
			If(CountInv("MP40Loaded_2") != Invoker.Ammo1.MaxAmount)
			{
				SetAmmoType1(True, "MP40Loaded_2");
				A_MagazineReload("MP40Loaded_2");
			}
			Else If(CountInv("MP40Loaded_1") != Invoker.Ammo1.MaxAmount)
			{
				SetAmmoType1(True, "MP40Loaded_1");
				A_MagazineReload("MP40Loaded_1");
			}
		}
		#### A 0 A_JumpIf(CountInv("MP40Loaded_1") == Invoker.Ammo1.MaxAmount && CountInv("MP40Loaded_2") == Invoker.Ammo1.MaxAmount || Invoker.Ammo2.Amount < 1, 1);
		Goto ReloadMags;
	ReloadFinish:
		WP3G C 1 { A_WeaponOffset(-3,57,WOF_INTERPOLATE); SetAmmoType1(True, "MP40Loaded_1"); }
		#### C 1 A_WeaponOffset(-3,59,WOF_INTERPOLATE);
		#### C 1 A_WeaponOffset(-3,63,WOF_INTERPOLATE);
		#### C 1 A_WeaponOffset(-3,67,WOF_INTERPOLATE);
		#### C 1 A_WeaponOffset(-3,65,WOF_INTERPOLATE);
		#### C 1 A_WeaponOffset(-3,62,WOF_INTERPOLATE);
		#### C 1 A_WeaponOffset(-3,58,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(-3,55,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(-2,53,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(-2,49,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(-2,46,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(-2,45,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(-1,44,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(-1,46,WOF_INTERPOLATE);
		#### D 1 A_WeaponOffset(-1,47,WOF_INTERPOLATE);
		#### D 1 { A_WeaponOffset(0,49,WOF_INTERPOLATE); A_WeaponReady(WRF_NOBOB); }
		#### A 1 { A_WeaponOffset(0,44,WOF_INTERPOLATE); A_WeaponReady(WRF_NOBOB); }
		#### A 1 { A_WeaponOffset(0,38,WOF_INTERPOLATE); A_WeaponReady(WRF_NOBOB); }
		#### A 1 { A_WeaponOffset(0,35,WOF_INTERPOLATE); A_WeaponReady(WRF_NOBOB); }
		#### A 1 { A_WeaponOffset(0,32,WOF_INTERPOLATE); A_WeaponReady(WRF_NOBOB); }
		Goto Ready;
	}
}

Class MP40Loaded_1 : Ammo
{
	Default
	{
		+INVENTORY.IGNORESKILL
		Inventory.MaxAmount 32;
	}
}
Class MP40Loaded_2 : Ammo
{
	Default
	{
		+INVENTORY.IGNORESKILL
		Inventory.MaxAmount 32;
	}
}
