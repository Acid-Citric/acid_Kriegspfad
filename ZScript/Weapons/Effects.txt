Class MyEmptyActor : Actor 
{
    Default 
    {
        +NOINTERACTION
        +NOBLOCKMAP
        +SYNCHRONIZED
        +DONTBLAST
        Radius 1;
        Height 1;
        FloatBobPhase 0;
    }
    Override Void PostBeginPlay() { Destroy(); }
}

Class WW_TracerBullet : FastProjectile // Credits to WildWeasel
{
	Default
	{
		Alpha 0.9;
		RenderStyle "Add";
		Radius 2;
		Height 2;
		Speed 65;
		PROJECTILE;
		+BLOODSPLATTER
		+DONTBLAST
		Scale 0.4;
		DamageFunction (5*random(1,3));
	}
	States
	{
	Spawn:
		PUFF A 1 BRIGHT A_SpawnParticle("FF9900", SPF_FULLBRIGHT|SPF_RELATIVE, 35, 5, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1.0, -1, -0.5);
		Loop;
	Death:
	Crash:
		PUFF A 2 BRIGHT
		{
			color col = Color(0xff,0x65+random(0,100),0x00);
			For(int i; i<4; i++)
			{
				A_SpawnParticle(
					col,
					SPF_FULLBRIGHT|SPF_RELATIVE|SPF_NOTIMEFREEZE,
					size: 5,
					angle: frandom[zpf](0, 359),
					xoff: frandom[zpf](-radius, radius),
					velx: frandom[zpf](0, 1),
					velz: frandom[zpf](1, 3),
					accelx: -(frandom[zpf](0, 1) / random[zpf](25, 40)),
					accelz: -0.1
				);
			}
		}
		PUFF B 2 BRIGHT A_SetTranslucent(0.9,0);
		PUFF CD 2 BRIGHT;
		Stop;
	XDeath:
		TNT1 A 3 A_StartSound("knife/slash");
		Stop;
	}
}

Class fx_realisticexplosion : Actor
{
	Default
	{
		+NOINTERACTION
		+NOGRAVITY
		+DONTSPLASH
		-NOTELEPORT
		Scale 2.0;
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay { bSPRITEFLIP = RandomPick(false, true); }
		B00M ABCD 2 Bright;
		B00M E 2 Bright; //A_Mushroom("fx_Mushroom", random(4,8), 0, frandom(4,8), 1.0);
		B00M FGHIJKLMNOPQ 2 Bright;
		Stop;
	}
}

Class fx_Mushroom : Actor
{
	Default
	{
		Speed 8;
		Damage 5;
		Scale 0.5;
		PROJECTILE;
		-NOGRAVITY
		//Scale 2.0;
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay { bSPRITEFLIP = RandomPick(false, true); }
		B00M A 2 Bright;
		Loop;
	Death:
		TNT1 A 0 A_Explode(64);
		Stop;
	}
}

Class HalfLife_Explosion_Small : fx_realisticexplosion
{
	Default	{ Scale 1.0; }
	States
	{
	Spawn:
		TNT1 A 0 NoDelay { For(int i; i<25; i++) { A_SpawnProjectile("WW_TracerBullet", 4, 0, random(0,360), CMF_ABSOLUTEPITCH|CMF_OFFSETPITCH|CMF_AIMDIRECTION, random(-40,40)); } }
		Goto fx_realisticexplosion::Spawn;
	}
}

Class HalfLife_Explosion_Medium : fx_realisticexplosion
{
	Default	{ Scale 1.5; }
	States
	{
	Spawn:
		TNT1 A 0 NoDelay { For(int i; i<125; i++) { A_SpawnProjectile("WW_TracerBullet", 4, 0, random(90,270), CMF_ABSOLUTEPITCH|CMF_OFFSETPITCH|CMF_AIMDIRECTION, random(-40,40)); } }
		Goto fx_realisticexplosion::Spawn;
	}
}

Class fx_realisticexplosion_s : fx_realisticexplosion {	Default	{ Scale 0.75; } }
