Class Dynamite_Proj : Actor
{
	Int timer, ticking;
	Default
	{
		Health 10;
		Damage (1);
		+MISSILE
		+SHOOTABLE
		+SOLID
		+NOBLOOD
		+DONTGIB
		+NOICEDEATH
		+NOTAUTOAIMED
		+DONTTHRUST
		//+FORCEXYBILLBOARD
		DamageFactor "Normal", 2.0;
		DamageFactor "Fire", 2.0;
	}
	Override Void Tick()
	{
		If(ticking >= 35)
		{
			A_StartSound("dynamite/bomb_ticker",CHAN_BODY,CHANF_DEFAULT,1);
			ticking = 0;
		}
		ticking++;

		Super.Tick();
		If(timer == 0) { SetStateLabel("Explode"); }
		timer--;
	}
	Override Bool Used(Actor User)
	{
		If(User && User.Player && timer > 0)
		{
			User.A_SpawnItemEx("AmmoTNT");
			Destroy();
		}
		Return Super.Used(User);
	}
	States
	{
	Spawn:
		TNT_ A 1;
		Wait;
	Death:
	//Death.Hitscan:
	//Death.Fire:
	Explode:
		TNT1 A 0
		{
			A_StartSound("granate/explode");
			A_Explode(256, 256, XF_HURTSOURCE, True, 192);
			A_SpawnItemEx("HalfLife_Explosion");
		}
		Stop;
	}
}

Class Granate_Thrown_A : Actor
{
	Int timer;
	Default
	{
		+ROLLSPRITE
		+ROLLCENTER
		+FORCEXYBILLBOARD

		Radius 4;
		Height 3;
		Speed 40;
		Damage (1);
		PROJECTILE;
		-NOGRAVITY
		-NOTELEPORT
		+CANBOUNCEWATER
		+BOUNCEONACTORS
		Obituary "%o failed to field %k's fragmenting fastball.";
		BounceType "Doom";
		BounceFactor 0.2;
		WallBounceFactor 0.6;
		BounceSound "grenade/bounce";
		SeeSound "granate/throw";
	}
	Override Void Tick()
	{
		Super.Tick();
		If(timer == 70) { SetStateLabel("Explode"); }
		timer++;
	}
	Override Void PostBeginPlay()
	{
		Super.PostBeginPlay();
			Self.bSPRITEFLIP = RandomPick(false, true);
	}
	States
	{
	Spawn:
		SHG_ X 1 A_SetRoll(Roll + 45.0, SPF_INTERPOLATE);
		Loop;
	Death:
		SHG_ X 0 A_SetRoll(RandomPick(-90, 90));
		SHG_ X 1;
		Wait;
	Explode:
		TNT1 A 0
		{
			A_StartSound("granate/explode");
			A_Explode(128, 128, XF_HURTSOURCE, True, 96);
			A_SpawnItemEx("HalfLife_Explosion_Small");
		}
		Stop;
	}
}

Class Granate_Thrown_B : Granate_Thrown_A
{
	States
	{
	Spawn:
		SHG_ Y 1 A_SetRoll(Roll + 45.0, SPF_INTERPOLATE);
		Loop;
	Death:
		SHG_ Y 1 A_SetRoll(Roll = RandomPick(-90, 90));
		Loop;
	Explode:
		TNT1 A 0
		{
			A_StartSound("granate/explode");
			A_Explode(256, 256, XF_HURTSOURCE, True, 192);
			A_SpawnItemEx("HalfLife_Explosion_Medium");
		}
		Stop;
	}
}
